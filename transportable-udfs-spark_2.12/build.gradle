apply plugin: 'scala'
apply plugin: 'com.github.alisiikh.scalastyle'
apply plugin: 'java-library'

java {
  toolchain.languageVersion.set(JavaLanguageVersion.of(8))
}

sourceSets {
  main {
    scala {
      srcDirs = project(':transportable-udfs-spark_2.11').sourceSets.main.scala.srcDirs
    }
  }
  test {
    scala {
      srcDirs = project(':transportable-udfs-spark_2.11').sourceSets.test.scala.srcDirs
    }
  }
}

dependencies {
  api project(':transportable-udfs-type-system')
  implementation project(':transportable-udfs-api')
  implementation project(':transportable-udfs-utils')
  // For spark-core and spark-sql dependencies, we exclude transitive dependency on 'jackson-module-paranamer',
  // since this is required for the LinkedIn version of spark-core and spark-sql.
  compileOnly(group: project.ext.'spark-group', name: 'spark-core_2.12', version: project.ext.'spark3-version') {
    exclude module: 'jackson-module-paranamer'
  }
  compileOnly(group: project.ext.'spark-group', name: 'spark-sql_2.12', version: project.ext.'spark3-version') {
    exclude module: 'jackson-module-paranamer'
  }
  compileOnly('com.fasterxml.jackson.module:jackson-module-paranamer:2.6.7')
  testImplementation(group: project.ext.'spark-group', name: 'spark-core_2.12', version: project.ext.'spark3-version') {
    exclude module: 'jackson-module-paranamer'
  }
  testImplementation(group: project.ext.'spark-group', name: 'spark-sql_2.12', version: project.ext.'spark3-version') {
    exclude module: 'jackson-module-paranamer'
  }
  testImplementation('com.fasterxml.jackson.module:jackson-module-paranamer:2.6.7')
  testImplementation project(path: ':transportable-udfs-type-system', configuration: 'tests')
}

task jarTests(type: Jar, dependsOn: testClasses) {
  archiveClassifier.set('tests')
  from sourceSets.test.output
}

configurations {
  tests {
    extendsFrom testRuntimeOnly
  }
}

artifacts {
  tests jarTests
}
