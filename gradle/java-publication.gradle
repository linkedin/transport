def licenseSpec = copySpec {
    from project.rootDir
    include "LICENSE"
}

tasks.register('sourcesJar', Jar) {
    from sourceSets.main.allJava
    archiveClassifier.set('sources')
    with licenseSpec
}

tasks.register('javadocJar', Jar) {
    from sourceSets.main.allJava
    archiveClassifier.set('javadoc')
    with licenseSpec
}

jar {
    with licenseSpec
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

apply plugin: "maven-publish" //https://docs.gradle.org/current/userguide/publishing_maven.html
publishing {
    publications {
        javaLibrary(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            artifactId = project.archivesBaseName

            pom {
                name = artifactId
                description = "A library for analyzing, processing, and rewriting views defined in the Hive Metastore, and sharing them across multiple execution engines"

                url = "https://github.com/linkedin/transport"
                licenses {
                    license {
                        name = 'BSD 2-CLAUSE LICENSE'
                        url = 'https://github.com/linkedin/transport/blob/master/LICENSE'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'wmoustafa'
                        name = 'Walaa Eldin Moustafa'
                    }
                    developer {
                        id = 'shardulm94'
                        name = 'Shardul Mahadik'
                    }
                }
                scm {
                    url = 'https://github.com/linkedin/transport.git'
                }
                issueManagement {
                    url = 'https://github.com/linkedin/transport/issues'
                    system = 'GitHub issues'
                }
                ciManagement {
                    url = 'https://travis-ci.com/linkedin/transport'
                    system = 'Travis CI'
                }
            }
        }
    }

    //useful for testing - running "publish" will create artifacts/pom in a local dir
    repositories { maven { url = "$rootProject.buildDir/repo" } }
}

apply plugin: 'signing' //https://docs.gradle.org/current/userguide/signing_plugin.html
signing {
    if (System.getenv("PGP_KEY")) {
        useInMemoryPgpKeys(System.getenv("PGP_KEY"), System.getenv("PGP_PWD"))
        sign publishing.publications.javaLibrary
    }
}