//Plugin jars are added to the buildscript classpath in the root build.gradle file
apply plugin: "org.shipkit.shipkit-auto-version" //https://github.com/shipkit/shipkit-auto-version

apply plugin: "org.shipkit.shipkit-changelog" //https://github.com/shipkit/shipkit-changelog
tasks.named("generateChangelog") {
  previousRevision = project.ext.'shipkit-auto-version.previous-tag'
  githubToken = System.getenv("GITHUB_TOKEN")
  repository = "linkedin/transport"
}

apply plugin: "org.shipkit.shipkit-github-release" //https://github.com/shipkit/shipkit-changelog
tasks.named("githubRelease") {
  def genTask = tasks.named("generateChangelog").get()
  dependsOn genTask
  repository = genTask.repository
  changelog = genTask.outputFile
  githubToken = System.getenv("GITHUB_TOKEN")
  newTagRevision = System.getenv("GITHUB_SHA")
}

apply plugin: "io.github.gradle-nexus.publish-plugin" //https://github.com/gradle-nexus/publish-plugin/
nexusPublishing {
  repositories {
    if (System.getenv("SONATYPE_PWD")) {
      sonatype {
        username = System.getenv("SONATYPE_USER")
        password = System.getenv("SONATYPE_PWD")
        nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
        snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
      }
    }
  }
}

// we need to exclude the plugin module for its specific gradle configuration
configure(allprojects - project(':transportable-udfs-plugin')) { p ->
  plugins.withId('java') {
    p.apply from: "$rootDir/gradle/java-publication.gradle"
  }
}
