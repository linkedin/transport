// Copyright 2019 LinkedIn Corporation. All rights reserved.
// Licensed under the BSD-2 Clause license.
// See LICENSE in the project root for license information.

buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath 'org.github.ngbinh.scalastyle:gradle-scalastyle-plugin_2.11:1.0.1'
  }
}

plugins {
  id "com.github.hierynomus.license" version "0.16.1"
}

allprojects {
  group = 'com.linkedin.transport'
  apply plugin: 'idea'
}

subprojects {
  buildscript {
    repositories {
      mavenCentral()
      jcenter()
    }
  }
  repositories {
    mavenCentral()
  }
}

subprojects {
  apply plugin: "com.github.hierynomus.license"

  license {
    header = file("${rootDir}/../gradle/license/LICENSE_HEADER")
    ext.year = Calendar.getInstance().get(Calendar.YEAR)
    skipExistingHeaders true
    ignoreFailures false
    strictCheck true
  }

  plugins.withType(JavaPlugin) {
    project.apply(plugin: 'checkstyle')

    dependencies {
      testImplementation 'org.testng:testng:6.11'
    }

    test {
      useTestNG()
    }

    checkstyle {
      configFile = rootProject.file('../gradle/checkstyle/checkstyle.xml')
      configProperties = [
          'configDir': rootProject.file('../gradle/checkstyle'),
          'baseDir': "${rootDir}/.."
      ]
      toolVersion '8.23'
    }
  }

  plugins.withType(ScalaPlugin) {
    project.apply(plugin: 'scalaStyle')

    scalaStyle {
      configLocation = "${rootDir}/../gradle/scalastyle/scalastyle-config.xml"
      source = sourceSets.main.allScala
      testSource = sourceSets.test.allScala
      includeTestSourceDirectory = true
    }
    check.dependsOn scalaStyle
  }
}
