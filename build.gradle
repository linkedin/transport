// Copyright 2019 LinkedIn Corporation. All rights reserved.
// Licensed under the BSD-2 Clause license.
// See LICENSE in the project root for license information.


buildscript {
  repositories {
    jcenter()
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.4'
    classpath 'org.github.ngbinh.scalastyle:gradle-scalastyle-plugin_2.11:1.0.1'
    classpath 'gradle.plugin.nl.javadude.gradle.plugins:license-gradle-plugin:0.14.0'
  }
}

plugins {
  id "org.shipkit.java" version "2.3.4"
  id "checkstyle"
}

allprojects {
  group = 'com.linkedin.transport'
  apply plugin: 'idea'
}

if (project.hasProperty('overrideBuildEnvironment')) {
  //The property is automatically passed to the Gradle build when the project is built at LinkedIn
  //The property contains the file path to a script plugin that 'adapts' this OS project to LinkedIn
  //In order to adapt the project, we need to apply this script plugin:
  apply from: project.overrideBuildEnvironment
} else {
  //If the property is not set the project is built outside of LinkedIn
  //Applying default build logic for an OS build:
  apply from: "defaultEnvironment.gradle"
}

subprojects {
  apply plugin: "com.github.hierynomus.license"

  license {
    header = file("${rootDir}/gradle/license/LICENSE_HEADER")
    ext.year = Calendar.getInstance().get(Calendar.YEAR)
    skipExistingHeaders true
    ignoreFailures false
    strictCheck true
  }

  configurations {
    all {
      // Transport as a library should only expose slf4j-api to its consumers and should not keep any SLF4J bindings
      // in its dependency graph
      // Quote from http://www.slf4j.org/faq.html#maven2
      //   "Thus, as far as your users are concerned you are exporting slf4j-api as a transitive dependency of your
      //    library, but not any SLF4J-binding or any underlying logging system."
      // Our dependencies (e.g. hadoop-common) do bring in slf4j-log4j12 in their dependency graph so we exclude them
      exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    }
  }

  plugins.withType(JavaPlugin) {
    project.apply(plugin: 'checkstyle')

    dependencies {
      testCompile 'org.testng:testng:6.11'
      testCompile 'org.slf4j:slf4j-simple:1.7.25'
    }

    test {
      useTestNG()
    }

    checkstyle {
      configFile = rootProject.file('gradle/checkstyle/checkstyle.xml')
      configProperties = [
          'configDir': rootProject.file('gradle/checkstyle'),
          'baseDir': rootDir
      ]
      toolVersion '8.23'
    }
  }

  plugins.withType(ScalaPlugin) {
    project.apply(plugin: 'scalaStyle')

    scalaStyle {
      configLocation = "${rootDir}/gradle/scalastyle/scalastyle-config.xml"
      source = sourceSets.main.allScala
      testSource = sourceSets.test.allScala
      includeTestSourceDirectory = true
    }
    check.dependsOn scalaStyle
  }
}
